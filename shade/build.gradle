apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion androidVersions.compileSdk
    buildToolsVersion androidVersions.buildTools

    compileOptions {
        def javaVersions = rootProject.ext.libraryJavaVersions
        sourceCompatibility = javaVersions.sourceCompatibility
        targetCompatibility = javaVersions.targetCompatibility
    }

    defaultConfig {
        minSdkVersion androidVersions.minSdk
        targetSdkVersion androidVersions.targetSdk
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile dependency.supportAnnotations

    testCompile dependency.junit
    testCompile dependency.robolectric
    testCompile dependency.assertjCore1
    testCompile dependency.assertjAndroid
    testCompile dependency.mockitoAll
}

jacoco {
    toolVersion = jacocoVersion
    reportsDir = file("${project.buildDir}/reports/jacoco")
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
    group = 'report'
    description = 'Generate unit test coverage report'

    reports {
        xml.enabled = true
        csv.enabled = false
        html.enabled = true
    }

    sourceDirectories = files(android.sourceSets.main.java.srcDirs)
    classDirectories = fileTree(
            dir: "${project.buildDir}/intermediates/classes/debug",
            excludes: [
                    '**/R.class',
                    '**/R$*.class',
                    '**/Manifest*.*',
                    'android/**/*.*',
                    '**/BuildConfig.*',
            ]
    )
    executionData = files("${project.buildDir}/jacoco/testDebugUnitTest.exec")
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publish {
    userOrg = metadata.userOrg
    repoName = metadata.repoName
    groupId = metadata.groupId
    artifactId = 'shade'
    description = 'Automatic code generation for the SharedPreferences operation'
    version = metadata.version
    website = metadata.website
    licences = metadata.licenses
}
